program = _{ SOI ~ TERMINATOR* ~ (stmt ~ TERMINATOR+) * ~ stmt? ~ EOI }

TERMINATOR = _{ NEWLINE | ";" }
WHITESPACE = _{ " " | "\t" }

integer = @{ ASCII_DIGIT+ }
identifier = @{ !keyword ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
keyword = @{
    "let"
}

add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
modulo = { "%" }

operators = _{
    add | sub | mul | div | modulo
}

call = {
    "(" ~ (expression ~ ",")* ~ expression? ~ ")"
}

infix = _{ expression_inner ~ (operators ~ expression_inner)+ }
suffix = { term ~ call+ }

term = _{ integer | identifier }

expression_inner = _{ suffix | term }
expression = { infix | expression_inner }

expression_stmt = { expression }
let_assign = { "let" ~ identifier ~ "=" ~ expression }

stmt = _{
    expression_stmt |
    let_assign
}
